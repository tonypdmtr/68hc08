#ifmain ;-----------------------------------------------------------------------
                    #ListOff
                    #Uses     mcu.inc
                    #ListOn
#endif ;------------------------------------------------------------------------
                    #Uses     upcase.sub
;*******************************************************************************
; Purpose: Compare buffers pointed to by HX and TOS (case sensitive)
; Input  : A = maximum number of bytes to compare
;        : HX -> start of first memory
;        : TOS -> start of second memory
; Output : N Z V C set or cleared in accordance with the CMP instruction
;        : Allows BEQ, BNE, BLO, BHS, etc. to be used on result just as if
;        : a regular CMPA instruction had been used.
; Note(s): All registers except N Z V C flags are preserved
;        : Violates Carry-Set on error rule.  Carry set is used for result (BLO, etc.)
;        : Stacked pointer not destroyed

StringCompareBuff   macro     [#]Buffer1 [#]Buffer2 [#]ByteSize
                    mset      #' '
                    mreq      1,2,3:[#]Buffer1 [#]Buffer2 [#]ByteSize
                    #push
                    #spauto   :sp
                    push
                    #psp
                    lda       ~3~
                    @@lea     ~2~
                    pshhx
                    @@_ldhx_  ~1~ 1,psp
                    call      ~0~
                    ais       #:psp
                    pull
                    #pull
                    endm
;-------------------------------------------------------------------------------
                    #spauto   :ab

StringCompBuff      proc
                    sec                           ;case-insensitive
                    bra       ?StringCompareBuff

;*******************************************************************************
; Purpose: Compare buffers pointed to by HX and TOS (case insensitive)
; Input  : A = maximum number of bytes to compare
;        : HX -> start of first memory
;        : TOS -> start of second memory
; Output : N Z V C set or cleared in accordance with the CMP instruction
;        : Allows BEQ, BNE, BLO, BHS, etc. to be used on result just as if
;        : a regular CMPA instruction had been used.
; Note(s): All registers except N Z V C flags are preserved
;        : Violates Carry-Set on error rule.  Carry set is used for result (BLO, etc.)
;        : Stacked pointer not destroyed

StringCompBuff      macro     [#]Buffer1 [#]Buffer2 [#]ByteSize
                    mset      #' '
                    mreq      1,2,3:[#]Buffer1 [#]Buffer2 [#]ByteSize
                    #push
                    #spauto   :sp
                    push
                    #psp
                    lda       ~3~
                    @@lea     ~2~
                    pshhx
                    @@_ldhx_  ~1~ 1,psp
                    call      ~0~
                    ais       #:psp
                    pull
                    #pull
                    endm
;-------------------------------------------------------------------------------
                    #spauto   :ab

StringCompareBuff   proc
                    clc                           ;case-sensitive (original)
;                   bra       ?StringCompareBuff

;*******************************************************************************

                    #spauto   :ab

?StringCompareBuff  proc
.str2@@             equ       1
                    psha      max_size@@
                    pshhx     .str1@@
                    #ais

                    tpa
                    psha      ccr@@               ;shorter PSHCC sequence
                    and       #CMP_NOT_AFFECTED_  ;mask off our CCR result bits
                    psha      ans@@               ;will combine with result CCR
          #ifhcs
                    ldhx      .str2@@,sp
                    pshhx     .str2@@
                    ldhx      .str1@@,sp
          #else
                    tsx
                    lda       .str2@@+1,spx
                    psha
                    lda       .str2@@,spx
                    psha      .str2@@,2

                    lda       .str1@@,spx
                    ldx       .str1@@+1,spx
                    tah
          #endif
                    lda       max_size@@,sp
                    beq       Done@@              ;zero length always OK

Loop@@              push                          ;save length & pointer to 1st memory

                    lda       ccr@@,sp
                    tap
                    lda       ,x                  ;get character at HX (does not affect CCR[C])
                    bcs       NoCase@@
          ;-------------------------------------- ;case-sensitive case (original)
          #ifhcs
                    ldhx      .str2@@,sp
          #else
                    ldx       .str2@@,sp
                    txh
                    ldx       .str2@@+1,sp
          #endif
                    cmpa      ,x                  ;is the same as the one at .str2@@,sp?
                    bra       Cont@@
          ;-------------------------------------- ;case-insensitive case
NoCase@@            call      Upcase
                    psha      a@@
          #ifhcs
                    ldhx      .str2@@,sp
          #else
                    tsx
                    lda       .str2@@,spx
                    ldx       .str2@@+1,spx
                    tah
          #endif
                    lda       ,x
                    call      Upcase
                    psha      b@@

                    tsx
                    lda       a@@,spx             ;is the same as the one at .str2@@,sp?
                    cmpa      b@@,spx             ;(in this order: a cmp b)

                    pula:2
          ;--------------------------------------
Cont@@              pull                          ;restore pointer to 1st memory & length
                    bne       Done@@              ;Not same, get out with result

                    dbnza     NextChar@@          ;repeat for all characters
                    bra       Done@@

NextChar@@          aix       #1                  ;No, let's go check next ones
                    @inc.s    .str2@@,sp Loop@@   ;increment target pointer
                    bra       Loop@@
          ;---------------------------------------------------------------------
          ; CMP affected flags always in correct state when coming to Done@@
          ;---------------------------------------------------------------------
Done@@              tsx
                    tpa                           ;A = actual CMP result [CCR]
                    and       #CMP_AFFECTED_      ;mask off unused CCR flags
                    ora       ans@@,spx           ;combine with our CCR result bits
                    tap                           ;set return CCR
          ;--------------------------------------
                    ais       #:ais
                    pull
                    rtc

;*******************************************************************************
                    #sp
;*******************************************************************************
