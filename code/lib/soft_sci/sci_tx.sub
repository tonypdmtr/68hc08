;*******************************************************************************
;* Module    : SCI_TX.SUB
;* Programmer: Tony Papadimitriou <tonyp@acm.org>
;* Purpose   : Software SCI TX (transmitter)
;* Language  : Motorola/Freescale/NXP HC08/9S08 Assembly Language (aspisys.com/ASM8)
;* Status    : Copyright (c) 2021 by Tony Papadimitriou <tonyp@acm.org>
;* Segments  : To be placed in user defined segment (MMU compatible)
;* Subsystems: None -- Fully software driven (bit-banged)
;*******************************************************************************

#ifmain ;-----------------------------------------------------------------------
                    #ListOff
                    #Uses     qd4.inc
                    #ListOn
SCI_TX_PIN          pin       PORTA,0
#endif ;------------------------------------------------------------------------

                    @CheckPin SCI_TX_PIN

?TX                 equ       SCI_TX_PIN

;*******************************************************************************
; To determine the value to use for each bps rate, you must divide the
; bus-clock (e.g., 8MHz) by the bps rate (e.g., 9600).  So, 8000000/9600=
; 833.33333 but we round to the closest integer which in this case is 833.
; So, for an 8MHz bus system, to get a bps rate of 9600, we use the value 833
;-------------------------------------------------------------------------------
; Note: This software driven SCI is more capable than a built-in SCI, found in
; most HC08s, in that it can produce pretty much any bps rate (within the speed
; limits of the MCU) using any crystal frequency.  All you have to do is come
; up with the correct values as described above.
;*******************************************************************************

BPS_RATE            def       9600
?bps_value          equ       BUS_HZ/BPS_RATE

;*******************************************************************************
; Purpose: Send char in RegA to the SCI
; Input  : A = character
; Output : None
; Notes  : Interrupts are disabled during transmission and restored afterwards

                    #spauto

SCI_PutChar         proc
                    psha      ch@@
                    pshhx
                    #ais

                    tpa
                    psha      ccr@@
                    sei                           ;disable interrupts during send

                    @On       ?TX                 ;insure data line is output high

                    lda       ch@@,sp             ;A = char to send
          ;--------------------------------------
                    ldx       #9                  ;number of data bits to send (including start bit)
                    bra       Zero@@              ;send the start bit (a zero)
          ;-------------------------------------- ;send the data bits
                              #Cycles
Loop@@              lsra                          ;SCI is lsb first
                    bcc       Zero@@

                    bset      ?TX                 ;a 'HIGH' bit
                    bra       Cont@@
                              #temp :cycles
Zero@@              bclr      ?TX                 ;a 'LOW' bit
                    brn       *                   ;(for timing symmetry)
                              #Cycles :temp
Cont@@              bsr       ?DelayBitTime       ;delay one bit time
                    dbnzx     Loop@@              ;not done yet, put another bit
                              #temp :cycles
          ;--------------------------------------
                    bset      ?TX                 ;send the stop bit (a one)
                    bsr       ?DelayBitTime
          ;--------------------------------------
          #iftos ccr@@
                    pula
          #else
                    lda       ccr@@,sp
                    ais       #:ais
          #endif
                    tap                           ;restore caller's CCR[I]
                    pull
                    rtc

;*******************************************************************************
; Purpose: Delay a full bit time (based on ?bps_value) including context code
; Input  : None
; Output : None
; Note(s):
                    #spauto
                              #Cycles :temp
?DelayBitTime       proc
                    pshhx
                    ldhx      #DELAY@@
                              #Cycles
Loop@@              @cop
                    aix       #-1
                    cphx      #0
                    bne       Loop@@
                              #temp :cycles
                    pulhx
                    rts

DELAY@@             equ       ?bps_value-:cycles-:ocycles/:temp

;*******************************************************************************
                    #sp
;*******************************************************************************
