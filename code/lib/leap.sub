;*******************************************************************************
;* Module    : LEAP.SUB
;* Programmer: Tony Papadimitriou <tonyp@acm.org>
;* Language  : Motorola/Freescale/NXP HC08/9S08 Assembly Language (aspisys.com/ASM8)
;* Purpose   : Jump to any PPAGE location (in #MMU mode), works as JMP otherwise
;* Status    : FREEWARE Copyright (c) 2022 by Tony Papadimitriou <tonyp@acm.org>
;* Original  : http://www.aspisys.com/code/hc08/leap.html
;* History   : 10.12.09 v1.00 Original
;*           : 13.11.27 v1.10 Allowed for use even in non-MMU mode
;*           : 21.12.29       Added #Error if placed inside the PPAGE window
;*******************************************************************************

#ifmain ;-----------------------------------------------------------------------
                    #ListOff
                    #Uses     qe128.inc
                    #ListOn
#endif ;------------------------------------------------------------------------
?_OBJECT_?
          #if ]]{:pc} <> 0
            #if [[{:pc} >= :PAGE_START
              #if ]]{:pc} <= :PAGE_END
                    #Error    Place this library outside PPAGE window
              #endif
            #endif
          #endif
;*******************************************************************************
; Purpose: Uses CALL to leap to any PPAGE (no return address remains on stack)
; Input  : 24-bit address stored in three bytes following the CALL instruction
; Output : None
; Note(s): It is assumed that the #MMU mode is active and this function is not
;        : inside a PPAGE window (and certainly not a different one from the caller's)
; Use    :          call      LEAP
;        :          far       SomeFarCode

                    #spauto

Leap                proc
pc@@                equ       ::,:ab              ;24-bit return address stacked by CALL
                    push
                    ldhx      pc@@+{:ab-2},sp     ;HX -> hardcoded "far" address
                    @mova.s   ,x pc@@,sp          ;copy address into return address
                    pull
                    RTC                           ;performs LEAP/JMP atomically

                    #sp
;*******************************************************************************
                    #Exit
;*******************************************************************************
                    @EndStats
                    #Message  Size: {:pc-Leap} bytes, {:cycles} cycles
